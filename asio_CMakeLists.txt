cmake_minimum_required(VERSION 3.20)
project(asio VERSION 1.36.0)  # Set the current ASIO version

# --- ASIO source ---
set(ASIO_INCLUDE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/asio/include")

# --- Create a header-only library ---
add_library(asio INTERFACE)

target_include_directories(asio INTERFACE
    $<BUILD_INTERFACE:${ASIO_INCLUDE_DIR}>
    $<INSTALL_INTERFACE:include>
)

# --- Export and install ---
include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

set(ASIO_INSTALL_CONFIGDIR ${CMAKE_INSTALL_LIBDIR}/cmake/asio)

install(TARGETS asio
    EXPORT asioTargets
)

# Install asio.hpp
install(FILES
    ${ASIO_INCLUDE_DIR}/asio.hpp
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

# Install asio folder
install(DIRECTORY ${ASIO_INCLUDE_DIR}/asio
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

install(EXPORT asioTargets
    FILE asioTargets.cmake
    NAMESPACE asio::
    DESTINATION ${ASIO_INSTALL_CONFIGDIR}
)

write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/asioConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

# Configuration file for find_package
file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/asioConfig.cmake"
"include(\"\${CMAKE_CURRENT_LIST_DIR}/asioTargets.cmake\")\n"
)

install(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/asioConfig.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/asioConfigVersion.cmake"
    DESTINATION ${ASIO_INSTALL_CONFIGDIR}
)
