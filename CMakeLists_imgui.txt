cmake_minimum_required(VERSION 3.20)
project(imgui VERSION 1.92.5)

set(IMGUI_SOURCES
    imgui.cpp
    imgui_demo.cpp
    imgui_draw.cpp
    imgui_tables.cpp
    imgui_widgets.cpp
    backends/imgui_impl_sdl3.cpp
    backends/imgui_impl_sdlrenderer3.cpp # Use SDL_Renderer by default
)

add_library(imgui STATIC ${IMGUI_SOURCES})
add_library(imgui::imgui ALIAS imgui)

target_include_directories(imgui PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/backends>
    $<INSTALL_INTERFACE:include>
    $<INSTALL_INTERFACE:include/imgui>
)

if(NOT TARGET SDL3::SDL3)
    find_package(SDL3 REQUIRED)
endif()

target_link_libraries(imgui
        PUBLIC
        $<BUILD_INTERFACE:SDL3::SDL3> # Internal visible
)

include(${THIRD_PARTY_ROOT}/InstallHelper.cmake)
setup_install_rules(
    TARGET imgui
    NAMESPACE imgui
    INCLUDE_DIRS "${CMAKE_CURRENT_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}/backends"
    INCLUDE_SUBDIR "imgui"
)