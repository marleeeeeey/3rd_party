cmake_minimum_required(VERSION 3.20)
project(uSockets LANGUAGES C CXX)

# Search dependencies
find_package(libuv REQUIRED)
find_package(OpenSSL REQUIRED)

if(TARGET libuv::uv_a)
    set(LIBUV_LIB libuv::uv_a) # Static version
elseif(TARGET libuv::uv)
    set(LIBUV_LIB libuv::uv)   # Dinamic version
elseif(TARGET uv_a)
    set(LIBUV_LIB uv_a)
elseif(TARGET uv)
    set(LIBUV_LIB uv)
else()
    message(FATAL_ERROR "Could not find any libuv target (uv, uv_a, libuv::uv, etc.)")
endif()

# Source files
add_library(uSockets STATIC
    src/bsd.c
    src/context.c
    src/loop.c
    src/socket.c
    src/udp.c

    src/crypto/openssl.c
    src/crypto/sni_tree.cpp

    src/eventing/libuv.c
)

# Definitions
target_compile_definitions(uSockets PUBLIC
    LIBUS_USE_LIBUV
    LIBUS_USE_OPENSSL
)

# Linkage
target_link_libraries(uSockets PRIVATE
    ${LIBUV_LIB}
    OpenSSL::SSL
    OpenSSL::Crypto
)

# Headers
target_include_directories(uSockets PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
    $<INSTALL_INTERFACE:include>
)

# Install rules
install(TARGETS uSockets EXPORT uSocketsTargets DESTINATION lib)
install(DIRECTORY src/ DESTINATION include FILES_MATCHING PATTERN "*.h" PATTERN "*.hpp")
install(EXPORT uSocketsTargets NAMESPACE uSockets:: DESTINATION lib/cmake/uSockets)

# Config file for find_package()
file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/uSocketsConfig.cmake"
    "include(CMakeFindDependencyMacro)\n"
    "find_dependency(libuv)\n"
    "find_dependency(OpenSSL)\n"
    "include(\"\${CMAKE_CURRENT_LIST_DIR}/uSocketsTargets.cmake\")\n"
)
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/uSocketsConfig.cmake" DESTINATION lib/cmake/uSockets)