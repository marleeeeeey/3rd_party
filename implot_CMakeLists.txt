cmake_minimum_required(VERSION 3.20)
project(implot VERSION 1.92.5)

# This is what implot sees when building itself. It expects imgui to be installed in the system.
list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/../../external_install/imgui")

set(IMPLOT_SOURCES
    implot.cpp
    implot_demo.cpp
    implot_items.cpp
)

add_library(implot STATIC ${IMPLOT_SOURCES})

target_include_directories(implot PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    $<INSTALL_INTERFACE:include>
    $<INSTALL_INTERFACE:include/implot>
)

find_package(imgui REQUIRED)

target_link_libraries(implot PUBLIC imgui::imgui)

# --- Export and Install ---

include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

set(IMPLOT_INSTALL_CONFIGDIR ${CMAKE_INSTALL_LIBDIR}/cmake/implot)

install(TARGETS implot
    EXPORT implotTargets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

install(FILES
    implot.h
    implot_internal.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/implot)

install(EXPORT implotTargets
    FILE implotTargets.cmake
    NAMESPACE implot::
    DESTINATION ${IMPLOT_INSTALL_CONFIGDIR}
)

write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/implotConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

# This is what other projects will see after installation
file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/implotConfig.cmake"
"include(CMakeFindDependencyMacro)
list(APPEND CMAKE_PREFIX_PATH \"\${CMAKE_CURRENT_LIST_DIR}/../../../imgui\")
find_dependency(imgui)
include(\"\${CMAKE_CURRENT_LIST_DIR}/implotTargets.cmake\")
")

install(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/implotConfig.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/implotConfigVersion.cmake"
    DESTINATION ${IMPLOT_INSTALL_CONFIGDIR}
)
