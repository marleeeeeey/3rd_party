cmake_minimum_required(VERSION 3.20)
project(imgui VERSION 1.92.5)

list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/../SDL3_installed")

set(IMGUI_SOURCES
    imgui.cpp
    imgui_demo.cpp
    imgui_draw.cpp
    imgui_tables.cpp
    imgui_widgets.cpp
    backends/imgui_impl_sdl3.cpp
    backends/imgui_impl_sdlrenderer3.cpp # Use SDL_Renderer by default
)

add_library(imgui STATIC ${IMGUI_SOURCES})

target_include_directories(imgui PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/backends>
    $<INSTALL_INTERFACE:include>
    $<INSTALL_INTERFACE:include/imgui>
)

find_package(SDL3 REQUIRED)

target_link_libraries(imgui PUBLIC SDL3::SDL3)

# --- Export and Install ---

include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

set(IMGUI_INSTALL_CONFIGDIR ${CMAKE_INSTALL_LIBDIR}/cmake/imgui)

install(TARGETS imgui
    EXPORT imguiTargets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

install(FILES
    imconfig.h
    imgui.h
    imgui_internal.h
    imstb_rectpack.h
    imstb_textedit.h
    imstb_truetype.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/imgui)

install(FILES 
    backends/imgui_impl_sdl3.h
    backends/imgui_impl_sdlrenderer3.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/imgui)

install(EXPORT imguiTargets
    FILE imguiTargets.cmake
    NAMESPACE imgui::
    DESTINATION ${IMGUI_INSTALL_CONFIGDIR}
)

write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/imguiConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/imguiConfig.cmake"
"include(CMakeFindDependencyMacro)
list(APPEND CMAKE_PREFIX_PATH \"\${CMAKE_CURRENT_LIST_DIR}/../../../SDL3_installed\")
find_dependency(SDL3)
include(\"\${CMAKE_CURRENT_LIST_DIR}/imguiTargets.cmake\")
")

install(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/imguiConfig.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/imguiConfigVersion.cmake"
    DESTINATION ${IMGUI_INSTALL_CONFIGDIR}
)
